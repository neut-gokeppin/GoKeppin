<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   xmlns:mx="library://ns.adobe.com/flex/mx" pageTitle="誤欠品検出力向上ツール - 従業員マスタメンテナンス"
			   creationComplete="application1_creationCompleteHandler(event)" xmlns:components="jp.co.ctc.components.*">
	<s:layout>
		<s:VerticalLayout paddingLeft="8" paddingRight="8" paddingTop="8" paddingBottom="8" horizontalAlign="center" gap="8"/>
	</s:layout>

	<fx:Style source="lfa_inspection.css"/>
	
	<!-- 2016/02/24 DA ins start -->
	<fx:Metadata>
		[ResourceBundle("message")]
		[ResourceBundle("resource")]
	</fx:Metadata>
	<!-- 2016/02/24 DA ins end -->
	
	<fx:Script>
		<![CDATA[
			import jp.co.ctc.entity.MUser;
			import jp.co.ctc.util.MUserUtils;
			import jp.co.ctc.util.Utils;
			
			import mx.charts.chartClasses.ChartItemDragProxy;
			import mx.collections.ArrayList;
			import mx.controls.Alert;
			import mx.controls.List;
			import mx.controls.advancedDataGridClasses.AdvancedDataGridItemRenderer;
			import mx.events.AdvancedDataGridEvent;
			import mx.events.CloseEvent;
			import mx.events.CollectionEvent;
			import mx.events.CollectionEventKind;
			import mx.events.FlexEvent;
			import mx.events.ListEvent;
			import mx.events.ScrollEvent;
			import mx.formatters.DateFormatter;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			private var rmenuItem:MUser;
			private var rmenuIndex:int = -1;
			private var rmenu:ContextMenu;
			private var rmenuitem1:ContextMenuItem;
			private var rmenuitem2:ContextMenuItem;
			// 2016/02/24 DA ins start
			private var rmenuitem3:ContextMenuItem;
			// 2016/02/24 DA ins end
			private var removedRows:ArrayCollection = new ArrayCollection();
			private var TITLE:String = "従業員マスタメンテ";

			private var hasNewRecodes:Boolean = false;//レコードの追加を行っているかどうか

			private var loginUser:String = "";
			// 2016/02/24 DA ins start
			public var gridScrollPosition:int = 0; // 表示行のインデックス
			public var loginUserInfo:MUser; //ログインユーザ情報
			public var gridRadioEditable:Boolean = false;	// ラジオボタンの編集状態
			// 2016/02/24 DA ins end

			/**
			 * 初期ロード時の処理。
			 * ユーザーコードを取得します。
			 */
			protected function application1_creationCompleteHandler(event:FlexEvent):void
			{
				// Debug用 ****************************************
				//　Debugログインさせ、ログインが行われたようにする。
				//srv.login("1");

				//ユーザーコードの取得
				srvUser.getUserCode();

				// Alertのボタンの幅を変更
				// デフォルトの60だと"キャンセル"の文字が表示しきれない。
				Alert.buttonWidth = 100;

				/**
				 * アプリケーション初期化
				 * ・コンテキストメニューの準備
				 */
				rmenu = new ContextMenu();
				rmenu.hideBuiltInItems();
				rmenu.addEventListener(ContextMenuEvent.MENU_SELECT, rmenu_menuSelectHandler);
				rmenuitem1 = new ContextMenuItem("行を追加");
				rmenuitem1.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, addRowHandler);
				rmenu.customItems.push(rmenuitem1);
				rmenuitem2 = new ContextMenuItem("行を削除");
				rmenuitem2.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, removeRowHandler);
				rmenu.customItems.push(rmenuitem2);
				// 2016/02/24 DA ins start
				rmenuitem3 = new ContextMenuItem("パスワードリセット");
				rmenuitem3.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, resetPassHandler);
				rmenu.customItems.push(rmenuitem3);
				// 2016/02/24 DA ins end

				// 2016/08/19 DA del start
				//初期ロードの全件表示
				//srv.getMUsersByKeys();
				// 2016/08/19 DA del start

				// 2016/02/24 DA del start
				////グリッドのメニューの設定
				//grid.contextMenu = rmenu;
				// 2016/02/24 DA del end
			}

			/**
			 * ユーザーコードの取得後の処理。
			 * ユーザコードがなければログインページに戻す
			 */
			protected function getUserCode_resultHandler(event:ResultEvent):void
			{
				//ユーザコードがなければログインページに戻す
				if (event.result==null) {
					var request:URLRequest = new URLRequest("login.html");
					navigateToURL(request, "_self");
				}
				else{
					this.loginUser = event.result.toString();

					// 2016/02/24 DA ins start
					// ログインユーザ情報を取得
					srvUser.getMUser(this.loginUser);
					// 2016/02/24 DA ins end
				}
			}

			// 2016/08/19 DA ins start
			/**
			 * 従業員マスタ取得成功時の処理。
			 */
			protected function getMUsersByKeys_resultHandler(event:ResultEvent):void
			{
				this.grid.verticalScrollPosition = this.gridScrollPosition;
				this.grid.initialize();
				this.grid.validateNow();
				this.grid.dataProvider.refresh();
			}
			// 2016/08/19 DA ins end
			
			/**
			 * 右クリックメニュー表示時の処理。
			 */
			private function rmenu_menuSelectHandler(event:ContextMenuEvent):void {
				// 右クリックされた行を保持しておく
				if (-1 < rmenuIndex && rmenuIndex < ac.length) {
					rmenuItem = ac.getItemAt(rmenuIndex) as MUser;
				} else {
					rmenuItem = null;
				}
			}
			/**
			 * 検索ボタン押下時の処理。
			 */
			protected function btnSerch_clickHandler(event:MouseEvent):void
			{
				//編集のチェック
				if (updataExists()) {
					// 編集あり
					var myAlert : Alert = Alert.show("変更が保存されていません。\n検索すると変更が破棄されます。よろしいですか？",
						TITLE,
						Alert.OK | Alert.CANCEL, null,
						function(event:CloseEvent):void {
							if (event.detail == Alert.OK) {
								getUserData();
								return;
							}
						},
						// 2016/08/29 DA ins start
						null,
						Alert.CANCEL
						// 2016/08/29 DA ins end
					);
				}
				else {
					getUserData();
				}
			}

			/**
			 * 入力された検索条件からを従業員データを取得します
			 */
			protected function getUserData():void
			{
				//入力された値を変数に格納
				var userCode:String = txtUserCode.text;
				var userName:String = txtUserName.text;
				var teamCode:String = txtTeamCode.text.toString();
				var deleteFlag:String = "";
				// 2016/02/24 DA ins start
				var passNotExpired:String = "";
				var passExpired:String = "";
				var masterAdmin:String = "";
				var officeOrganization:String = "";
				var officeOrganizationNot:String = "";
				var examiner:String = "";
				// 2016/02/24 DA ins end

				//削除フラグチェックの有無
				if(chkDelete.selected){
					deleteFlag = "1";
				}else{
					deleteFlag = "";
				}
				
				// 2016/02/24 DA ins start
				if(this.chkNotExpired.selected){
					passNotExpired = "1";
				}else{
					passNotExpired = "";
				}
				
				if(this.chkExpired.selected){
					passExpired = "1";
				}else{
					passExpired = "";
				}
				
				if(this.chkMasterAdmin.selected){
					masterAdmin = "1";
				}else{
					masterAdmin = "";
				}
				
				if(this.chkOfficeOrganization.selected){
					officeOrganization = "1";
				}else{
					officeOrganization = "";
				}
				
				if(this.chkOfficeOrganizationNot.selected){
					officeOrganizationNot = "1";
				}else{
					officeOrganizationNot = "";
				}
				
				if(this.chkExaminer.selected){
					examiner = "1";
				}else{
					examiner = "";
				}
				// 2016/02/24 DA ins end
				
				// 2016/02/24 DA upd start
				srv.getMUsersByKeys(userCode,userName,teamCode,deleteFlag, passNotExpired, passExpired, masterAdmin, officeOrganization, officeOrganizationNot, examiner);
				
				// 2016/08/19 DA del start
				//this.gridScrollPosition = this.grid.verticalScrollPosition;
				//this.grid.initialize();
				//this.grid.dataProvider.refresh();
				// 2016/08/19 DA del end
				// 2016/02/24 DA upd end
			}

			/**
			 * 行追加の処理
			 */
			private function addRowHandler(item:ContextMenuEvent):void {
				// 右クリックした行番号を取得
				// 行選択チェック
				if (rmenuItem == null) {
					return;
				}
				var mUser:MUser = rmenuItem;
				var row:int = ac.getItemIndex(mUser);

				var user:MUser = new MUser();
				// 2016/02/24 DA ins start
				user.userCode = "";
				user.userName = "";
				user.teamCode = "";
				user.postName = "";
				user.passDate = Utils.inputTextToDate("1900/01/01");
				user.passDateExpire = Utils.inputTextToDate("1900/01/01");
				user.authority = "0"; // 検査員
				// 2016/02/24 DA ins end
				//Flagの初期値を0で格納、作成者も格納
				user.deleteFlag = "0";
				user.insertUser = loginUser;
				user.editDiv = "I";

				ac.addItemAt(user, row + 1);
				// 追加した行を選択状態にする
				grid.selectedIndex = row + 1;
				
			}

			/**
			 * 行削除の処理
			 */
			private function removeRowHandler(item:ContextMenuEvent):void
			{
				// 右クリックした行番号を取得
				// 行選択チェック
				if (rmenuItem == null) {
					return;
				}
				var mUser:MUser = rmenuItem;
				var row:int = ac.getItemIndex(mUser);

				var user:MUser = ac[row];

				// 新規追加した行でなければ、削除した行を削除用配列に退避
				if(user.editDiv != "I")
				{
					Alert.show("新規追加した行以外は削除できません",TITLE);

				// 新規追加した行は単純に削除
				}else{
					ac.removeItemAt(row);
				}
			}
			
			// 2016/02/24 DA upd start
			/**
			 * パスワードリセットの処理
			 */
			private function resetPassHandler(item:ContextMenuEvent):void
			{
				// 右クリックした行番号を取得
				// 行選択チェック
				if (rmenuItem == null) {
					return;
				}
				var mUser:MUser = rmenuItem;
				var row:int = ac.getItemIndex(mUser);
				// パスワードリセットした行を選択状態にする
				grid.selectedIndex = row;
				
				var user:MUser = ac[row];
				
				user.passDate = Utils.inputTextToDate("1900/01/01");
				user.passDateExpire = Utils.inputTextToDate("1900/01/01");
				user.passWord = user.userCode;
				user.editDiv = "U";
				user.updateUser = loginUser;
			}
			// 2016/02/24 DA upd end

			/**
			 * グリッドの値を持つArrayの値が変更された時の処理。
			 * グリッドの新規追加・削除・更新に対応した処理を行います。
			 */
			protected function ac_collectionChangeHandler(event:CollectionEvent):void
			{
				if(event.kind==CollectionEventKind.UPDATE){

					var editDiv:String = (ac[grid.selectedIndex] as MUser).editDiv;

					if (editDiv != "I") {
						// 新規に追加した行でなければ編集区分にU:変更をセットし、
						// 更新者にログインユーザーをセット
						(ac[grid.selectedIndex] as MUser).editDiv = "U";
						(ac[grid.selectedIndex] as MUser).updateUser = loginUser;
					}
				}
				// 2016/02/24 DA ins start
				else if (event.kind==CollectionEventKind.RESET) {
					this.grid.verticalScrollPosition = this.gridScrollPosition;
					this.grid.initialize();
					// 2016/08/19 DA ins start
					this.grid.validateNow();
					// 2016/08/19 DA ins end
					this.grid.dataProvider.refresh();
				}
				// 2016/02/24 DA ins end
			}

			/**
			 * 終了ボタン押下時の処理。
			 */
			private function btnExit_clickHandler(event:MouseEvent):void{

				// 編集あり
				if (updataExists()) {
					var myAlert : Alert = Alert.show("変更が保存されていません。終了してもよろしいですか？",
						TITLE,
						Alert.OK | Alert.CANCEL, null, alertCloseHandler,
						// 2016/08/29 DA ins start
						null,
						Alert.CANCEL
						// 2016/08/29 DA ins end
					);
					return;
				}

				// Browserを閉じる
				closeMyBrowser();
				return;
			}

			/**
			 * 変更の有無をチェックします
			 * @return true:変更有　false:変更無
			 */
			private function updataExists():Boolean{
				// 変更有無チェック
				for (var loop_cnt:int =0;loop_cnt < ac.length; loop_cnt++) {
					var user:MUser = ac[loop_cnt];
					if (user.editDiv != null) {
						return  true;
					}
				}
				return false;
			}

			/**
			 * 終了確認ダイアログの結果処理
			 */
			private function alertCloseHandler(e:CloseEvent) : void
			{
				if (e.detail == Alert.OK) {
					closeMyBrowser();
					return;
				}
			}
			/**
			 * ブラウザの終了
			 */
			private function closeMyBrowser():void
			{
				// 機能選択画面に遷移する
				var request:URLRequest = new URLRequest("function_menu.html");
				navigateToURL(request, "_self");
			}

			/**
			 * 保存ボタン押下時の処理。
			 */
			protected function btnUpdate_clickHandler(event:MouseEvent):void
			{

				var user:MUser;
				var editFlag:Boolean = false;
				var loop_cnt:int;

				this.hasNewRecodes = false;

				// 変更有無チェック
				if (removedRows.length == 0) {
					for ( loop_cnt =0;loop_cnt < ac.length; loop_cnt++) {
						user = ac[loop_cnt];
						if (user.editDiv != null) {
							editFlag = true;
							break;
						}
					}
					if (editFlag==false) {
						Alert.show("保存する内容がありません",TITLE);
						return;
					}
				}

				for ( loop_cnt =0;loop_cnt < ac.length; loop_cnt++) {
					user = ac[loop_cnt];

					if(user.editDiv == null){
						continue;
					}
					//値をトリムして格納（Nullの時は空白を代入）
					var userCode:String = Utils.jTrimToEmpty(user.userCode);
					var userName:String = Utils.jTrimToEmpty(user.userName);
					var teamCode:String = Utils.jTrimToEmpty(user.teamCode);
					var postName:String = Utils.jTrimToEmpty(user.postName);


					//新規追加時の従業員コードチェック
					if(user.editDiv=="I"){
						this.hasNewRecodes = true;

						if(userCode.length == 0){
							Utils.gridSelectRow(grid,loop_cnt);
							Alert.show("従業員コードが入力されていません。",TITLE);
							return;
						}

						//英数字チェックを実行(正しければ英字を大文字に変換)
						if(!char_NumEng_check(userCode)){
							Utils.gridSelectRow(grid,loop_cnt);
							return;
						}else{

							userCode = userCode.toUpperCase();

						}

						//コードの重複チェック
						for(var i:int=0; i<ac.length ;i++){
							if(loop_cnt == i){
								continue;
							}
							var user2:MUser = ac[i];
							var user2Code:String = Utils.jTrimToEmpty(user2.userCode);
							if(userCode==user2Code){
								Utils.gridSelectRow(grid,loop_cnt);
								Alert.show("この従業員は既に存在します。",TITLE);
								return;
							}
						}
					}

					//名前のチェック
					if (userName.length ==0){
						Utils.gridSelectRow(grid,loop_cnt);
						Alert.show("名前が入力されていません。",TITLE);
						return;
					}
					if (userName.length > 30){
						grid.scrollToIndex(loop_cnt);
						Alert.show("名前を30文字以下にしてください。",TITLE);
						return;
					}

					//組コードのチェック
					if (teamCode.length > 5){
						grid.scrollToIndex(loop_cnt);
						Alert.show("組コードを５文字以下にしてください。",TITLE);
						return;
					}
					//組コードの入力チェック
					for (i=0;i<teamCode.length;i++) {
						var char_teamCode:String = teamCode.charAt(i)
						if(char_teamCode.match(/[0-9A-Za-z]/) == null){
							grid.scrollToIndex(loop_cnt);
							Alert.show("組が正しくありません。",TITLE);
							return;
						}
					}
					//部署名チェック
					if (postName.length > 30){
						grid.scrollToIndex(loop_cnt);
						Alert.show("部署名を30文字以下にしてください。",TITLE);
						return;
					}
					//チェックが通ればその値を格納
					user.userCode = userCode;
					user.userName = userName;
					user.teamCode = teamCode;
					user.postName = postName;
					ac[loop_cnt] = user;
				}

				// 2016/02/24 DA ins start
				// 表示行を一時退避する
				this.gridScrollPosition = this.grid.verticalScrollPosition;
				// 2016/02/24 DA ins end
				
				// 変更内容をデータベースに反映
				srv.updateAll(ac);
				
				// 2016/02/24 DA upd start
				// データベースから最新のデータを取得
				// srv.getMUsersByKeys();
				this.getUserData();
				// 2016/02/24 DA upd end
				
				// removedRowsを初期化
				removedRows = new ArrayCollection();

			}

			/**
			 * 従業員コードの英数字チェック。
			 * @return true:チェックOK　false:チェックNG
			 */
			private function char_NumEng_check(userCode:String):Boolean
			{
				var user:String = userCode;
				//英数字の7文字がチェック
				// 2014/10/27 DA upd start
				//if(user.length != 7 || user.match(/[0-9A-Za-z]{7}/) == null){
				var userMatch:Array = user.match(/[0-9A-Za-z]{1,7}/);
				if(userMatch == null || user.length > 7 || user.length != userMatch[0].length){ 
					// 2014/10/27 DA upd end
					Alert.show("従業員コードが正しくありません。",TITLE);
					return false;
				}
				return true;
			}


			/**
			 * Data更新正常完了
			 */
			private function onUpdateAllResult(e:ResultEvent):void
			{
				var msg:String = "";
				if (hasNewRecodes) {
					msg = "保存が完了しました。\n\n※追加した従業員のパスワードは従業員コードです。\n　ログイン時に必ず変更してください。\n\n";
				} else {
					msg = "保存が完了しました。";
				}
				Alert.show(msg,TITLE);


//				Alert.show("保存が完了しました。",TITLE, Alert.OK, this,
//					function(event:CloseEvent):void {
//						if(hasNewRecodes) {
//							Alert.show("追加した従業員のパスワードは従業員コードです。\nログイン時に必ず変更してください。", TITLE);
//						}
//					}
//				);
			}

			/**
			 * Data更新Error Event
			 */
			private function onUpdateAllFault(e:FaultEvent):void
			{
				Alert.show("保存に失敗しました。",TITLE);
			}

			/**
			 * グリッド上のデータにマウスが乗ったときの処理
			 */
			protected function grid_itemRollOverHandler(event:ListEvent):void
			{
				// マウスカーソルの乗っている行を保持しておく。
				// 右クリックで使用する
				rmenuIndex = event.rowIndex;
			}

			/**
			 * グリッドを編集時の処理。
			 */
			protected function grid_itemEditBeginHandler(event:AdvancedDataGridEvent):void
			{
				// 新規追加行以外の従業員コードを編集したときは処理を無効にする
				if (event.columnIndex == 0 && MUser(event.itemRenderer.data).editDiv != "I")
				{
					event.preventDefault();
				}
			}
			
			// 2016/02/24 DA ins start
			/**
			 * グリッドのパスワード登録日欄に表示する文字列を取得
			 */
			protected function grid_passDate_labelFunction(data:MUser, column:AdvancedDataGridColumn):String {
				
				if (data.passDateExpire == null) {
					return null;
				}
				
				var formatter:DateFormatter = new DateFormatter();
				formatter.formatString = 'YYYY/MM/DD';
				return formatter.format(data.passDateExpire);
			}


			protected function getMUser_resultHandler(event:ResultEvent):void {
				this.loginUserInfo = event.result as MUser;
				this.enabledComponent();

				// 2016/08/19 DA ins start
				this.getUserData();
				// 2016/08/19 DA ins end
			}
			
			/**
			 * コンポーネントの制御をする
			 */
			private function enabledComponent():void
			{
				if (MUserUtils.isAdministrator(this.loginUserInfo)) {
					// マスタ管理者
					this.grid.editable = 'true';
					this.btnUpdate.enabled = true;
					this.gridRadioEditable = true;
					
					//グリッドのコンテキストメニューの設定
					this.grid.contextMenu = rmenu;
				} else {
					// 以外
					this.grid.editable = 'false';
					this.btnUpdate.enabled = false;
					this.gridRadioEditable = false;
				}
			}

			/**
			 * スクロール時に正しいデータを表示する
			 */
			protected function grid_scrollHandler(event:ScrollEvent):void
			{
				this.gridScrollPosition = this.grid.verticalScrollPosition;
			}
			// 2016/02/24 DA ins end
			
		]]>
	</fx:Script>

	<fx:Declarations>
		<s:RemoteObject id="srv" destination="MUserService">
			<s:method name="getMUsersByKeys" result="getMUsersByKeys_resultHandler(event)"/>
			<s:method name="updateAll" result="onUpdateAllResult(event)" fault="onUpdateAllFault(event)"/>
		</s:RemoteObject>
		<s:RemoteObject id="srvUser" destination="MUserService">
			<s:method name="getUserCode" result="getUserCode_resultHandler(event)"/>
			<s:method name="getMUser" result="getMUser_resultHandler(event)"/>
		</s:RemoteObject>
		<s:ArrayCollection id="ac" collectionChange="ac_collectionChangeHandler(event)" list="{grid.dataProvider as IList}" />
	</fx:Declarations>
	<s:HGroup width="100%" height="24" verticalAlign="middle" fontFamily="ＭＳ ゴシック">
		<s:Label text="従業員マスタメンテナンス" fontSize="14" fontWeight="bold"/>
	</s:HGroup>
	<s:HGroup width="100%" height="24" verticalAlign="middle" fontFamily="ＭＳ ゴシック">
		<s:Label text="従業員コード" width="125" verticalAlign="middle"/>
		<s:TextInput id="txtUserCode" maxChars="7" width="125" tabIndex="1"/>
		<s:Label width="25"/>
		<s:Label text="名前" width="100" verticalAlign="middle"/>
		<s:TextInput id="txtUserName" width="125" tabIndex="2"/>
		
		<!-- 2016/02/24 DA ins start -->
		<s:Label width="25"/>
		
		<!-- Pass期限 -->
		<s:Label text="{resourceManager.getString('resource','cli0700001')}" width="60" verticalAlign="middle"/>
		
		<!-- 中-->
		<s:CheckBox width="50" label="{resourceManager.getString('resource','cli0700002')}" id="chkNotExpired" selected="true" tabIndex="2"/>
		
		<!-- 切 -->
		<s:CheckBox width="50" label="{resourceManager.getString('resource','cli0700003')}" id="chkExpired" selected="true" tabIndex="2"/>
		<!-- 2016/02/24 DA ins end -->
	</s:HGroup>
	<s:HGroup width="100%" height="24" verticalAlign="middle" fontFamily="ＭＳ ゴシック">
		<s:Label width="125" text="組コード" verticalAlign="middle"/>
		<s:TextInput id="txtTeamCode" maxChars="5" width="125" tabIndex="3"/>
		<s:Label width="25" text=""/>
		<s:CheckBox width="100" label="削除" id="chkDelete" tabIndex="4"/>

		<!-- 2016/02/24 DA ins start -->
		<!-- マスタ管理者 -->
		<s:CheckBox width="100" label="{resourceManager.getString('resource','cli0700005')}" id="chkMasterAdmin" selected="true" tabIndex="4"/>
		
		<!-- 検査職制本番可 -->
		<s:CheckBox width="120" label="{resourceManager.getString('resource','cli0700006')}" id="chkOfficeOrganization" selected="true" tabIndex="4"/>
		
		<!-- 検査職制本番不可 -->
		<s:CheckBox width="120" label="{resourceManager.getString('resource','cli0700007')}" id="chkOfficeOrganizationNot" selected="true" tabIndex="4"/>
		
		<!-- 検査員 -->
		<s:CheckBox width="100" label="{resourceManager.getString('resource','cli0700008')}" id="chkExaminer" selected="true" tabIndex="4"/>
		<!-- 2016/02/24 DA ins end -->

		<s:Button label="検索"  id="btnSerch" fontSize="12" height="30" click="btnSerch_clickHandler(event)" tabIndex="5"/>
	</s:HGroup>


	<mx:AdvancedDataGrid id="grid" designViewDataType="flat" width="100%" height="100%"
						 dataProvider="{srv.getMUsersByKeys.lastResult}"
						 editable="true" sortExpertMode="true" tabIndex="6"  draggableColumns="false"
						 itemRollOver="grid_itemRollOverHandler(event)" itemEditBegin="grid_itemEditBeginHandler(event)"
						 scroll="grid_scrollHandler(event)">
		<mx:groupedColumns>
			<mx:AdvancedDataGridColumn headerText="従業員コード" dataField="userCode" sortable="true"
									   itemRenderer="jp.co.ctc.components.BackgroundColorItemRenderer"/>
			<mx:AdvancedDataGridColumn headerText="名前" dataField="userName" sortable="true"
									   itemRenderer="jp.co.ctc.components.BackgroundColorItemRenderer"/>
			<mx:AdvancedDataGridColumn headerText="組コード" dataField="teamCode"
									   itemRenderer="jp.co.ctc.components.BackgroundColorItemRenderer"/>
			<mx:AdvancedDataGridColumn headerText="部署名" dataField="postName" sortable="true" 
									   itemRenderer="jp.co.ctc.components.BackgroundColorItemRenderer"/>
			<!-- 2016/02/24 DA ins start -->
			<!-- パスワード登録日 -->
			<mx:AdvancedDataGridColumn headerText="{resourceManager.getString('resource','cli0700001')}" dataField="passDateExpire" sortable="true" width="80"
									   backgroundColor="{resourceManager.getUint('resource','nonEditableColor')}" editable="false" textAlign="center" labelFunction="grid_passDate_labelFunction" />

			<!-- アクセス権限 -->
			<mx:AdvancedDataGridColumnGroup headerText="{resourceManager.getString('resource','cli0700004')}">
				
				<mx:AdvancedDataGridColumn headerText="{resourceManager.getString('resource','cli0700005')}" editable="false" sortable="false" dataField="authority" textAlign="center" width="120">
					<mx:itemRenderer>
						<fx:Component>
							<mx:RadioButton textAlign="center" groupName="authorityGroup_{this.listData.rowIndex}" value="3" selected="{data.authority == value}" click="{if (this.outerDocument.gridRadioEditable) data.authority = value}" enabled="{this.outerDocument.btnUpdate.enabled}" />
						</fx:Component>
					</mx:itemRenderer>
				</mx:AdvancedDataGridColumn>
				
				<mx:AdvancedDataGridColumn headerText="{resourceManager.getString('resource','cli0700006')}" editable="false" sortable="false" dataField="authority" textAlign="center" width="120">
					<mx:itemRenderer>
						<fx:Component>
							<mx:RadioButton textAlign="center" groupName="authorityGroup_{this.listData.rowIndex}" value="2" selected="{data.authority == value}" click="{if (this.outerDocument.gridRadioEditable) data.authority = value}" enabled="{this.outerDocument.btnUpdate.enabled}" />
						</fx:Component>
					</mx:itemRenderer>
				</mx:AdvancedDataGridColumn>
				
				<mx:AdvancedDataGridColumn headerText="{resourceManager.getString('resource','cli0700007')}" editable="false" sortable="false" dataField="authority" textAlign="center" width="120">
					<mx:itemRenderer>
						<fx:Component>
							<mx:RadioButton textAlign="center" groupName="authorityGroup_{this.listData.rowIndex}" value="1" selected="{data.authority == value}" click="{if (this.outerDocument.gridRadioEditable) data.authority = value}" enabled="{this.outerDocument.btnUpdate.enabled}" />
						</fx:Component>
					</mx:itemRenderer>
				</mx:AdvancedDataGridColumn>
				
				<mx:AdvancedDataGridColumn headerText="{resourceManager.getString('resource','cli0700008')}" editable="false" sortable="false" dataField="authority" textAlign="center" width="120">
					<mx:itemRenderer>
						<fx:Component>
							<mx:RadioButton textAlign="center" groupName="authorityGroup_{this.listData.rowIndex}" value="0" selected="{data.authority == value}" click="{if (this.outerDocument.gridRadioEditable) data.authority = value}" enabled="{this.outerDocument.btnUpdate.enabled}" />
						</fx:Component>
					</mx:itemRenderer>
				</mx:AdvancedDataGridColumn>
			</mx:AdvancedDataGridColumnGroup>
			<!-- 2016/02/24 DA ins end -->
			<mx:AdvancedDataGridColumn headerText="削除" dataField="deleteFlag"
									   itemRenderer="jp.co.ctc.components.BackgroundColorItemRenderer" visible="false"/>
			<mx:AdvancedDataGridColumn headerText="削除" editable="false" sortable="false" width="50"
									   itemRenderer="jp.co.ctc.components.CheckBox" dataField="deleteFlag"/>
		</mx:groupedColumns>
	</mx:AdvancedDataGrid>
	
	<s:HGroup width="100%" height="24" verticalAlign="middle" horizontalAlign="center" fontFamily="ＭＳ ゴシック">
		<s:Button id="btnUpdate" height="30" label="保存" fontSize="12" click="btnUpdate_clickHandler(event)" tabIndex="7"/>
		<s:Button id="btnExit" height="30" label="終了" fontSize="12" click="btnExit_clickHandler(event)" tabIndex="8"/>
	</s:HGroup>

</s:Application>